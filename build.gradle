plugins {
    id 'groovy'
    id 'java'
    id 'application'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "io.freefair.lombok" version "8.6"
}

wrapper {
    gradleVersion = '8.7'
    distributionType = Wrapper.DistributionType.BIN
}

group 'com.dscalzi'
version '2.2.0'

description = 'Utility library to extract metadata through bytecode analysis.'
mainClassName = 'com.dscalzi.claritas.Main'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {

    implementation "com.google.code.findbugs:jsr305:${jsr305Version}"

    implementation "org.ow2.asm:asm:${asmVersion}"
    implementation "org.ow2.asm:asm-commons:${asmVersion}"

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"

    implementation "net.sf.jopt-simple:jopt-simple:${joptsimpleVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

processResources {
    from 'LICENSE.txt'
}

license {
    header = rootProject.file('HEADER.txt')
    ext {
        name = 'Daniel D. Scalzi'
        year = '2020'
        url = 'https://github.com/dscalzi/Claritas'
    }
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Implementation-Version': getArchiveVersion(), 'Specification-Version': getArchiveVersion()
    }
}

shadowJar {
    archiveClassifier.set('dist')
}
assemble.dependsOn('shadowJar')